buildscript {
	repositories {
	  jcenter()
	  mavenCentral()
	}
	dependencies {
	  classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'    // latest App Engine Gradle tasks
		classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
	}
}

apply plugin: 'java'	                            // standard Java tasks
apply plugin: 'com.google.cloud.tools.appengine'	// App Engine tasks
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
	  // Use jcenter for resolving your dependencies.
	  // You can declare any Maven/Ivy/file repository here.
	  jcenter()
	  mavenCentral()
}

dependencies {
		compile 'com.google.appengine:appengine-api-1.0-sdk:+'	// Latest App Engine Api's
		implementation 'com.google.code.gson:gson:2.8.6'
		compile 'com.google.guava:guava:27.0.1-jre'

	  // Use JUnit test framework
	  testImplementation 'junit:junit:4.12'
}

allprojects {
	gradle.projectsEvaluated {
	  tasks.withType(JavaCompile) {
	    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
	  }
	}
}

// Always run unit tests before deployment
appengineDeploy.dependsOn test
appengineStage.dependsOn test
appengineDeploy.dependsOn shadowJar
appengineStage.dependsOn shadowJar

appengine {
	stage {
		artifact = "build/libs/ruleshaus-all.jar"
	}
}

test {
	useJUnit()
	testLogging.showStandardStreams = true
	afterTest { descriptor, result ->
	  logger.lifecycle("test: " + descriptor + ": " + result )
	}
}

jar {
	manifest {
		attributes('Main-Class': 'haus.rules.Main')
	}
}

sourceCompatibility = 1.11	   // App Engine Flexible uses Java 11
targetCompatibility = 1.11	   // App Engine Flexible uses Java 11
appengine.deploy.projectId = 'rulehaus'
appengine.deploy.version = '1'
